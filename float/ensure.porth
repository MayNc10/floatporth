//make sure that everything works
include "fcore.porth"
include "arith.porth"
include "trig.porth"

proc main in 
  //check non-essential fcore functions (essential functions are tofloat/toint and the getters/setters, and are assumed to work
  1 tofloat fNeg -1 tofloat = 
  1 tofloat dup fAbs = land 
  -zero fAbs +zero = land 
  -zero +zero fCopysign +zero = land 
  -1 tofloat fIsNeg land 
  19 tofloat fIsPos land 
  25 tofloat fSign 1 tofloat = land 
  -zero fSign -1 tofloat = land 
  7 tofloat dup 8 tofloat fNextAfter 6 fNextAfter = land
  1 tofloat fIsNaN lnot land
  qNaN fIsNaN land
  if 
    "PASSED: " puts
  else
    "FAILED: " puts
  end
  "Sign manipulation test\n" puts

  //test arithmatic
  1 tofloat 2 tofloat fAdd toint 3 = 
  1 tofloat 1 tofloat fSub toint 0 = land
  1 tofloat -1 tofloat fAdd toint 0 = land
  
  if
    "PASSED: " puts
  else
    "FAILED: " puts
  end
  "Addition/Subtraction test\n" puts
  
  //Test mul
  1 tofloat 5 tofloat fMul toint 5 = 
  46 tofloat 5 tofloat fMul toint 230 = land
  67 tofloat 0 tofloat fMul toint 0 = land
  0 tofloat dup fMul toint 0 = land
  -1 tofloat 32 tofloat fMul toint -32 = land
  -1 tofloat -32 tofloat fMul toint 32 = land
 
  if
    "PASSED: " puts
  else
    "FAILED: " puts
  end
  "Multiplication Test\n" puts

  //test division
  18 tofloat 3 tofloat fDiv toint 6 =
  1 tofloat 2 tofloat fDiv 1056964608 = land
  1 tofloat 10 tofloat fDiv 1036831949 = land //test rounding

  if
    "PASSED: " puts
  else
    "FAILED: " puts
  end

  "Division test\n" puts
  
  //Test comparisons
  1 tofloat 5 tofloat f< 
  1 tofloat 5 tofloat f<= land
  5 tofloat 1 tofloat f< lnot land
  67 tofloat 54 tofloat f<= lnot land
  20 tofloat 9 tofloat f> land
  20 tofloat 9 tofloat f>= land
  7 tofloat 34 tofloat f> lnot land
  7 tofloat 34 tofloat f>= lnot land
  4 tofloat dup f>= land
  4 tofloat dup f<= land
  4 tofloat dup f= land
  6 tofloat 5 tofloat f!= land
  +zero -zero f= land 
  qNaN qNaN f= lnot land
  qNaN qNaN f!= land
  qNaN 1 f> lnot land
  qNaN 1 f< lnot land
  qNaN 1 f<= lnot land
  qNaN 1 f>= lnot land
  inf 33554432 tofloat f> land
  -inf -33554432  tofloat f< land

  if
    "PASSED: " puts
  else
    "FAILED: " puts
  end
  "Comparison test\n" puts
end
