//make sure that everything works
include "fcore.porth"
include "arith.porth"
include "trig.porth"
include "exp.porth"
include "log.porth"

proc check bool in
  if 
    "PASSED: " puts
  else
    "FAILED: " puts
  end
end

proc main in 
  //check non-essential fcore functions (essential functions are tofloat/toint and the getters/setters, and are assumed to work
  1 tofloat fNeg -1 tofloat = 
  1 tofloat dup fAbs = land 
  -zero fAbs +zero = land 
  -zero +zero fCopysign +zero = land 
  -1 tofloat fIsNeg land 
  19 tofloat fIsPos land 
  25 tofloat fSign 1 tofloat = land 
  -zero fSign -1 tofloat = land 
  7 tofloat dup 8 tofloat fNextAfter 6 fNextAfter = land
  1 tofloat fIsNaN lnot land
  qNaN fIsNaN land

  check
  "Sign manipulation test\n" puts

  1 tofloat toint 1 = 
  1067869798 toint 1 = land //1.3, round down
  1072064102 toint 2 = land //1.7, round up
  1069547520 toint 2 = land //1.5, round to even
  1075838976 toint 2 = land //2.5, rte

  check
  "Integer conversion test\n" puts

  //test arithmatic
  1 tofloat 2 tofloat fAdd toint 3 = 
  1 tofloat 1 tofloat fSub toint 0 = land
  1 tofloat -1 tofloat fAdd toint 0 = land
  0 0 fAdd 0 = land
  //1061158912 1056964608 fAdd 1067450368 = land //0.75 + 0.5 = 1.25
  1059061760 1052770304 fAdd toint 1 = land //0.625 + 0.375 = 1

  check
  "Addition/Subtraction test\n" puts
  
  //Test mul
  1 tofloat 5 tofloat fMul toint 5 = 
  46 tofloat 5 tofloat fMul toint 230 = land
  67 tofloat 0 tofloat fMul toint 0 = land
  0 tofloat dup fMul toint 0 = land
  -1 tofloat 32 tofloat fMul toint -32 = land
  -1 tofloat -32 tofloat fMul toint 32 = land
 
  check
  "Multiplication Test\n" puts

  //test division
  18 tofloat 3 tofloat fDiv toint 6 =
  1 tofloat 2 tofloat fDiv 1056964608 = land
  1 tofloat 10 tofloat fDiv 1036831949 = land //test rounding

  check
  "Division test\n" puts
  
  //Test comparisons
  1 tofloat 5 tofloat f< 
  1 tofloat 5 tofloat f<= land
  5 tofloat 1 tofloat f< lnot land
  67 tofloat 54 tofloat f<= lnot land
  20 tofloat 9 tofloat f> land
  20 tofloat 9 tofloat f>= land
  7 tofloat 34 tofloat f> lnot land
  7 tofloat 34 tofloat f>= lnot land
  4 tofloat dup f>= land
  4 tofloat dup f<= land
  4 tofloat dup f= land
  6 tofloat 5 tofloat f!= land
  +zero -zero f= land 
  qNaN qNaN f= lnot land
  qNaN qNaN f!= land
  qNaN 1 f> lnot land
  qNaN 1 f< lnot land
  qNaN 1 f<= lnot land
  qNaN 1 f>= lnot land
  inf 33554432 tofloat f> land
  -inf -33554432 tofloat f< land

  check
  "Comparison test\n" puts

  //Test indentification
  56 tofloat fIsPos
  56 tofloat fIsNeg lnot land
  -4 tofloat fIsNeg land
  -4 tofloat fIsPos lnot land
  qNaN fIsNaN land
  76 tofloat fIsNaN lnot land
  -76 tofloat fIsNaN lnot land 
  -34 tofloat qNaN eitherNan swap drop swap drop land
  qNaN dup eitherNan swap drop swap drop land
  -5 tofloat 27 tofloat eitherNan swap drop swap drop lnot land

  check
  "Identification test\n" puts
   
  //Test power functions
  4 tofloat 2 fPowI toint 16 = 
  5 tofloat 3 fPowI toint 125 = land
  2 tofloat 5 fPowIEBS toint 32 = land

  check
  "Power Test\n" puts

  e fln toint 1 =
  1 tofloat fln 0 = land
  2 tofloat fln 1060205080 = land

  check
  "Logarithm test\n" puts

  
end
